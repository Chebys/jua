表达式
======

表达式可以被求值。

可以划分为[初等表达式](#初等表达式)、[二元表达式](#二元表达式)、[赋值表达式](#赋值表达式)。

	expr ::= unassign_expr | assign_expr
	unassign_expr ::= primary | bin_expr

## 初等表达式

	primary ::= varname | keyword_expr | literal_num | literal_str | enclosure | obj_expr | array_expr | func_expr | prop_ref | optional_prop_ref | meth_wrapper | optional_meth_call | subscription | call | uni_expr | ter_expr
<!-- 变量名
关键字表达式
字面数字
字面字符串
括号表达式
字面对象
字面数组
字面函数
属性引用
可选属性引用
方法包装
可选方法调用
抽取
调用
一元表达式
三元表达式 -->

### 变量名
	varname ::= <是单词，且不是关键字（见词法）>
变量名会以 ascii 编码转换为 jua 字符串，作为属性键。

求值时，即对当前作用域进行属性访问（见抽象操作）。若属性不存在则抛出错误。

赋值时，会在当前作用域链上查找具有对应属性的作用域，若不存在则抛出错误，否则设置作用域的属性值。

声明时，对当前作用域进行属性设置。
* 目前，可以重复声明已有变量。
* 在多层级的语句块内部频繁访问外部变量会导致性能低下。用局部变量保存经常使用的值是个好习惯。
* 或者引入严格模式：该模式下使用词法作用域。参见[作用域问题](杂项.md#作用域问题)。

### 关键字表达式
	keyword_expr ::= 'true' | 'false' | 'null' | 'local'
`local` 用于获取当前作用域。

### 字面数字
	literal_num ::= <见词法>

### 字面字符串
	literal_str ::= <见词法>

### 括号表达式
	enclosure ::= '(' expr ')'
会直接解析为 `expr`。括号是用于语法分组的，将任何表达式结合为一个初等表达式。

### 字面对象
	obj_expr ::= '{' {prop_dec ','} [prop_dec] '}'
花括号包围、逗号分隔的[属性声明](#属性声明)列表。

可以求值为对象。规则与js中的字面对象几乎一致，只是把 `:` 换成 `=`。

### 属性声明
	prop_dec ::= varname | prop_key '=' expr | meth_dec
`{name}` 等价于 `{name=name}`

### 属性键
	prop_key ::= word | '[' expr ']'
`{name=name}` 等价于 `{['name']=name}`
### 方法声明
	meth_dec ::= prop_key func_body
`{meth(...){...}}` 等价于 `{['meth']=fun(...){...}}`

### 字面数组
	array_expr ::= '[' ']' | '[' flex_expr_list [','] ']'

### 灵活表达式列表
	flex_expr_list ::= expr_list | star_expr | flex_expr_list ',' star_expr
<!--和源码不太一样，这里非空、不允许尾随逗号-->

### 表达式列表
	expr_list ::= expr {',' expr}

### 字面函数
	func_expr ::= 'fun' func_body | 'fun' par_list '=' expr
`fun(x) = x+1` 等价于 `fun(x){ return x+1 }`
* 当 `fun` 位于语句开头时，会解析为[函数声明](语句.md#函数声明)，导致语法错误。

求值时，将当前作用域作为函数的父作用域。

### 函数尾
	func_body ::= par_list block

### 参数列表
	par_list ::= '(' [flex_dec_list] ')'

### 灵活声明列表
	flex_dec_list ::= dec_list | [dec_list ','] star_expr
* 语法上非空，运行时可空。

可以提供值的序列进行声明。提供的值可以多于[声明项](#声明项)的个数，此时若带有[星号表达式](#星号表达式)，则将多余值构成数组并赋予星号表达式中的变量。

若提供的值少于声明项的个数，则未接收到值的声明项必须具有初始值，否则报错。

### 声明列表
	dec_list ::= {dec_item ','} dec_item

### 声明项
	dec_item ::= dec_body | dec_body '?' | dec_body '=' expr
声明项由[可声明体](#可声明体)（通常是[变量名](#变量名)）和初始值（可选）组成。

第一种形式没有初始值。（除非用于[变量声明语句](语句.md#变量声明语句)）

最后一种形式中，`expr` 表示显式规定的初始值。

第二种形式表示自动添加初始值。
* 具体来说，若未显式规定初始值，则将初始值设为 `null`；若可声明体为[解构对象](#解构对象) / [解构数组](#解构数组)，则递归地对子声明项自动添加初始值。
* 例如，`{a, b=1, c as {d, e}}?` 等价于 `{a=null, b=1, c as {d=null, e=null}=null}=null`。

对声明项进行声明，可以提供目标值，若未提供则使用初始值。声明前，外部调用者需要检查并负责报错。
* 对于参数列表、解构对象、解构数组中的声明项，可以将初始值视作默认值。

### 可声明体
	dec_body ::= varname | left_obj | left_array
可以使用一个值（称作“目标值”，实际上可能是声明项的初始值）对可声明体进行声明。
* 可声明体一定是[左值](#左值)。

### 解构对象
	left_obj ::= '{' left_prop {',' left_prop} [','] '}'
不能为空。

赋值时，会从目标值中访问对应键的属性赋给子声明项。若属性不存在则使用子声明项的初始值。若均不存在则报错。

声明时类似。

### 解构属性声明
	left_prop ::= varname | varname '?' | varname '=' expr | prop_key 'as' dec_item
总是解析为 `prop_key 'as' dec_item`。

### 解构数组
	left_array ::= '[' flex_dec_list [','] ']'

### 星号表达式
	star_expr ::= '*' varname
用于[表达式列表](#表达式列表)时，表示展开一个可迭代对象。

用于[灵活声明列表](#灵活声明列表)时，表示接收多余参数到（新构造的）数组。

### 属性引用
	prop_ref ::= primary '.' word
求值时，进行属性访问，若属性不存在则报错。

赋值时，若左式为对象，则设置对象属性，否则报错。

### 可选属性引用
	optional_prop_ref ::= primary '?.' word
求值时，进行属性访问，若属性不存在则得到 null。

### 方法包装
	meth_wrapper ::= primary ':' word
相当于 `fun(*args) = primary.word(primary, *args)`，只是 `primary` 不会被多次求值。

### 可选方法调用
	optional_meth_call ::= primary '?:' word '(' [flex_expr_list] ')'
* 方法调用可以分解为方法包装和调用，但可选方法调用不可分解。

### 抽取
	subscription ::= primary '[' expr ']'
求值时，依次对 `primary` 和 `expr` 求值，得到 target 和 key。

若 target 为对象且不具有 getItem 元方法，则 key 必为字符串（否则报错），此时按照[可选属性引用](#可选属性引用)的规则求值。

否则， target 必须具有 getItem 元方法（否则报错）。求值规则相当于 getItem(target, key)。

赋值时，略。

### 调用
	call ::= primary '(' [flex_expr_list] ')'
			| primary <字面字符串开头的表达式>
			| primary func_body
			| primary block
后两种形式表示使用[字面函数](#字面函数)作为唯一参数。

	try{
		trysomething
	}:catch(err){
		dosthwitherr
	}
等价于

	try(fun(){ trysomething }):catch(fun(err){ dosthwitherr })

求值时，进行[函数调用](抽象操作.md#函数调用)，得到函数返回值。
<!-- 没有初始值的参数需要显式传入，否则报错；传入的参数可以多于声明的参数，会忽略 -->

### 一元表达式
	uni_expr ::= uni_oper primary
参见[运算符](词法.md#运算符)。
### 三元表达式
	ter_expr ::= 'if' '(' expr ')' expr 'else' expr

## 二元表达式
	bin_expr ::= unassign_expr bin_oper unassign_expr

参见[运算符](词法.md#运算符)。
* 显然，如果不考虑优先级，可以有多种匹配方式

## 赋值表达式
	assign_expr ::= left_val '=' expr | left_expr assign_oper expr
前一种形式求值时，对右式求值并赋予左式；后一种形式求值时，对左式、右式进行二元运算，并将得到的值赋予左式。

表达式的值为最终赋予左式的值。

### 左值
	left_val ::= left_expr | left_obj | left_array
能被赋值的语法结构。
* 左值是语法上的概念，并不是真正的值。
### 左值表达式
	left_expr ::= varname | prop_ref | subscription
* 可选属性引用？

既能求值也能赋值。
### 赋值号
	assign_oper ::= '=' | '+=' | '-=' | '*=' | '/=' | '&&=' | '||='
* 赋值号不是运算符，而是分隔符。这么规定有很大好处：任何两个表达式都可以用任意二元运算符连接，并且都是左结合的。
* 不同于 js，逻辑运算赋值号的短路作用于运算而不是赋值。
	* 也就是说，即使短路也会进行赋值，并触发元方法（如果有）